// Search for users who clicked on ZIP/MOV Domains
// Description: This query searches for users who clicked on URLs with ZIP/MOV domains in their emails.
// Reference: https://techcommunity.microsoft.com/t5/microsoft-defender-for-endpoint/defender-for-office-365-and-microsoft-defender-for-endpoint/ba-p/2266241
// Mitre ATT&CK Techniques: T1204
UrlClickEvents
| extend domain = tostring(parse_url(Url).Host)
| where domain endswith ".zip" or domain endswith ".mov"
| project Timestamp, NetworkMessageId, Clicked_Url = Url
| join EmailEvents on NetworkMessageId
| project Timestamp, Clicked_Url, RecipientEmailAddress, SenderMailFromAddress, SenderFromAddress, Subject, AttachmentCount, UrlCount

// Search for domain extensions that end with ZIP/MOV domains.

DeviceNetworkEvents
| extend domain = tostring(extract("https?://([^:/]*)(:?)(/|$)", 1, RemoteUrl))
| where domain endswith ".zip" or domain endswith ".mov"

// Detect devices that are blocked from accessing ZIP/MOV domains

DeviceNetworkEvents
| where Timestamp >= ago(1h)
| extend domain = tostring(extract("https?://([^:/]*)(:?)(/|$)",1,RemoteUrl))
| where domain endswith ".zip" or domain endswith ".mov"
| where ActionType != @"ConnectionSuccess"
