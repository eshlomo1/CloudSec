// Possible Ransomware Related Destruction Activity
// Description: This query looks for common ransomware-related destruction activity, such as clearing shadow copies, wiping drive free space, disabling system restore, deleting USN journal, re-ACLing files, and deleting shadow copies using PowerShell.
// Reference: https://docs.microsoft.com/en-us/windows-server/storage/file-server/protect-your-files/deploying-the-shadow-copies-feature
// Mitre ATT&CK: https://attack.mitre.org/techniques/T1486/
SentinelOne_CL
| where TimeGenerated > ago(1d)
| extend ProcessCommandLine = tostring(parse_json(DataFields_s).sourceprocesscommandline)
| extend FileName = tostring(parse_json(DataFields_s).sourceprocessfilepath)
| where (FileName =~ 'vssadmin.exe' and ProcessCommandLine has "delete shadows" and ProcessCommandLine has "/all" and ProcessCommandLine has "/quiet" ) // Clearing shadow copies
or (FileName =~ 'cipher.exe'
and ProcessCommandLine contains "/w") // Wiping drive free space
or (FileName =~ 'schtasks.exe'
and ProcessCommandLine has "/change" and ProcessCommandLine has @"\Microsoft\Windows\SystemRestore\SR" and ProcessCommandLine has "/disable") // Disabling system restore task
or (FileName =~ 'fsutil.exe'
and ProcessCommandLine has "usn" and ProcessCommandLine has "deletejournal" and ProcessCommandLine has "/d") // Deleting USN journal
or (FileName =~ 'icacls.exe'
and ProcessCommandLine has @'"C:\*"' and ProcessCommandLine contains '/grant Everyone:F') // Attempts to re-ACL all files on the C drive to give everyone full control
or (FileName =~ 'powershell.exe' and (
ProcessCommandLine matches regex @'\s+-((?i)encod?e?d?c?o?m?m?a?n?d?|e|en|enc|ec)\s+' and replace(@'\x00','', base64_decode_tostring(extract("[A-Za-z0-9+/]{50,}[=]{0,2}",0 , ProcessCommandLine))) matches regex @".*(Win32_Shadowcopy).*(.Delete\(\)).*"
) or ProcessCommandLine matches regex @".*(Win32_Shadowcopy).*(.Delete\(\)).*"
) // This query looks for PowerShell-based commands used to delete shadow copies